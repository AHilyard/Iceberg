buildscript {
	repositories {
		maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url = "https://repo.spongepowered.org/maven" }
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'

archivesBaseName = name + '-' + mcVersion

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

mixin {
	add sourceSets.main, "${project.name.toLowerCase()}.refmap.json"
}

minecraft {
	mappings channel: 'official', version: project.mcVersion

	runs {
		client = {
			properties 'forge.logging.markers': ''
			properties 'forge.logging.console.level': 'debug'
			properties 'terminal.ansi': 'true'
			workingDirectory project.file('run').canonicalPath
			source sourceSets.main

			arg "-mixin.debug=true"
			arg "-mixin.checks.interfaces=true"
			arg "-mixin.config=${project.name}.mixins.json".toLowerCase()
		}
	}
}

sourceSets {
	main {
		resources {
			srcDir 'src/generated/resources'
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${project.mcVersion}-${project.forgeVersion}"
	annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
}

jar {
	manifest {
		attributes(["Specification-Title": project.name,
					"Specification-Vendor": project.author,
					"Specification-Version": "24.0",
					"Implementation-Title": project.name,
					"Implementation-Version": project.version,
					"Implementation-Vendor" : project.author,
					"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
					"MixinConfigs": "${project.name.toLowerCase()}.mixins.json"],)
	}
}
